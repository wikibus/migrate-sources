output r2rml

template brochureId "https://sources.test.wikibus.org/brochure/{0}"
template brochureLocationId "https://sources.wikibus.org/storage-location/{0}"
template fileCabinetId "https://sources.test.wikibus.org/file-cabinet/{0}"
template brochurePdfId "https://sources.wikibus.org/brochure/{0}/file"
template legacyImageId "brochure-image-{0}"
template languageId "http://lexvo.org/id/iso639-1/{0}"
template userId "https://users.wikibus.org/user/{0}"
template date "{0}-{1}-{2}"

map Brochure from wikibus.brochures {
	subject template brochureId with Id;

	types
		wbo.Source
		wbo.Brochure

	properties
		bibo.pages from Pages with datatype xsd.int;
		opus.year from Year with datatype xsd.gYear;
		opus.month from Month with datatype xsd.gMonth;
		dcterms.date template date with Year Month Day;
		wba.storageLocation template brochureLocationId with Id;
		dcterms.language template languageId with Language;
		dcterms.title from FolderName;
		dcterms.description from Notes;
		dcterms.identifier from FolderCode;
		schema.contributor template userId with User;
		schema.associatedMedia template brochurePdfId with Id;
		schema.image template legacyImageId with Id as BlankNode;
		schema.primaryImageOfPage template legacyImageId with Id as BlankNode;
}

map BrochureLocation from wikibus.brochures {
	subject template brochureLocationId with Id;

	properties
		dcterms.identifier from FileCabinet;
		dtype.position from FileOffset with datatype xsd.int;
}

map BrochureFile from wikibus.brochures {
	subject template brochurePdfId with Id;

	types
		schema.MediaObject

	properties
		schema.contentSize from ContentSize with datatype xsd.long;
		schema.encodingFormat constant "application/pdf";
}

map BrochureLegacyImage from wikibus.brochures {
	subject template legacyImageId with Id as BlankNode;

    types
        schema.ImageObject

	properties
		schema.contentUrl template "https://wikibus-sources.herokuapp.com/brochure/{0}/image" with Id as Literal;
		schema.thumbnail template "brochure-image-thumb-{0}" with Id as BlankNode;
}

map BrochureImageThumbnail from wikibus.brochures {
	subject template "brochure-image-thumb-{0}" with Id as BlankNode;

    types
        schema.ImageObject

	properties
		schema.contentUrl template "https://wikibus-sources.herokuapp.com/brochure/{0}/image/small" with Id as Literal;
}