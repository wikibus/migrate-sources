output r2rml

template brochureId "https://sources.test.wikibus.org/brochure/{0}"
template imageId "https://sources.wikibus.org/image/{0}"

map BrochureImage from wikibus.images {
    subject template brochureId with SourceId;

    properties
        schema.image template imageId with ExternalId;
}

map BrochurePrimaryImage from wikibus.primaryImages {
    subject template brochureId with SourceId;

    properties
        schema.primaryImageOfPage template imageId with ExternalId;
}

map Image from wikibus.images {
    subject template imageId with ExternalId;

    types
        schema.ImageObject

    properties
        schema.contentUrl from OriginalUrl with datatype schema.URL;
        schema.thumbnail template "cloudinary-image-{0}" with Id as BlankNode;
        dtype.orderIndex from OrderIndex with datatype xsd.int;
}

map ImageThumbnail from wikibus.images {
    subject template "cloudinary-image-{0}" with Id;

    types
        schema.ImageObject

    properties
        schema.contentUrl from ThumbnailUrl with datatype schema.URL;
}