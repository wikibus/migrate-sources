output r2rml

template bookImageId "book/{0}/image"

map Book from wikibus.books {
	subject template bookId with Id;

	//graphs
	//	template bookId with Id;

	types
		wbo.Source
		wbo.Book
		schema.Book
		bibo.Book
		hydra.Resource

	properties
		bibo.pages from Pages;
		opus.year from Year with datatype xsd.gYear;
		dcterms.description from Notes;
		dcterms.title from BookTitle;
		schema.isbn from BookISBN;
		dcterms.language template languageId with Language;
		schema.image template bookImageId with LegacyImage;
		schema.primaryImageOfPage template bookImageId with LegacyImage;
		schema.author template "book-{0}-author" with Id as BlankNode;
		dcterms.created from DateCreated;
		dcterms.modified from DateUpdated;
		wba.images template sourceImageCollection with Id;
}

map BookOwners from wikibus.books {
	subject template userId with User;

	properties
		schema.owns template bookId with Id;
}

map BookAuthors from wikibus.books {
	subject template "book-{0}-author" with Id as BlankNode;

	properties
		schema.name from BookAuthor;
}

map BookLegacyImage from wikibus.books {
	subject template bookImageId with LegacyImage;

    types
        schema.ImageObject

	properties
		schema.contentUrl template "https://wikibus-sources.herokuapp.com/book/{0}/image" with LegacyImage as Literal;
		schema.thumbnail template "book/{0}/image/small" with LegacyImage;
}

map BookImageThumbnail from wikibus.books {
	subject template "book/{0}/image/small" with LegacyImage;

    types
        schema.ImageObject

	properties
		schema.contentUrl template "https://wikibus-sources.herokuapp.com/book/{0}/image/small" with LegacyImage as Literal;
}
